# Snakefile: Complete HCMV RNA-seq & Assembly Pipeline
# Steps:
# 1. Extract CDS from genome
# 2. Build kallisto index
# 3. Quantify transcripts (kallisto)
# 4. Sleuth differential expression
# 5. Build Bowtie2 genome index
# 6. Map reads
# 7. Count mapped reads (shell script generates output)
# 8. SPAdes transcriptome assembly
# 9. Extract longest contig
# 10. BLAST longest contig
# 11. Merge final report

# Sample List

SAMPLES = ["SRR5660030", "SRR5660033", "SRR5660044", "SRR5660045"]


# Files to merge at end

MERGE_INPUTS = [
    "../OUTPUTS/TEMP/cd.txt",
    "../OUTPUTS/TEMP/count.txt",
    "../OUTPUTS/TEMP/sleuth_report.txt",
] + expand("../OUTPUTS/TEMP/{sample}.blast.txt", sample=SAMPLES)



# RULE ALL â€“ Final pipeline targets

rule all:
    input:
        "../OUTPUTS/CDS/hcmv_cds.fasta", # Extracted CDS sequences
        "../OUTPUTS/INDEX/hcmv.idx", # Kallisto index
        expand("../OUTPUTS/QUANT/{sample}", sample=SAMPLES), # Kallisto quantification outputs
        "../SCRIPTS/temp.txt",
        expand("../OUTPUTS/MAPPING/{sample}.bam", sample=SAMPLES), # Bowtie2 mapping outputs
        expand("../OUTPUTS/BOWTIE2_INDEX/hcmv.{suffix}.bt2", 
               suffix=["1", "2", "3", "4", "rev.1", "rev.2"]), # Bowtie2 index files
        "../OUTPUTS/TEMP/count.txt", # Read count report from shell script
        "../OUTPUTS/TEMP/sleuth_report.txt", # Sleuth analysis results
        expand("../OUTPUTS/LONGEST_CONTIG/{sample}.fasta", sample=SAMPLES), # Longest contigs from SPAdes assembly
        expand("../OUTPUTS/TEMP/blast_{sample}.done", sample=SAMPLES), # BLAST completion flags
        "../PipelineReport.txt" # Final merged report


# 1. Extract CDS sequences from genome

rule extract_cds:
    input:
        fasta="../GCF_000845245.1/genomic.fna", # Genome FASTA file
        gff="../GCF_000845245.1/genomic.gff" # Genome annotation GFF file
    output: 
        cds="../OUTPUTS/CDS/hcmv_cds.fasta", # Output FASTA file for extracted CDS sequences
        report="../OUTPUTS/TEMP/cd.txt" # Report file for logging CDS extraction details
    shell:
        """
        mkdir -p ../OUTPUTS/CDS ../OUTPUTS/TEMP

        python ../SCRIPTS/extract_cds.py \
            -i {input.fasta} \
            -a {input.gff} \
            -o {output.cds} \
            -c {output.report}
        """


# 2. Build kallisto index

rule build_index:
    input:
        "../OUTPUTS/CDS/hcmv_cds.fasta" # Input FASTA file containing CDS sequences
    output:
        "../OUTPUTS/INDEX/hcmv.idx" # Kallisto index file
    shell:
        """
        mkdir -p ../OUTPUTS/INDEX
        kallisto index --make-unique \
            -i {output} {input}
        """



# 3. Kallisto quantification (per sample)

rule kallisto_quant:
    input:
        index="../OUTPUTS/INDEX/hcmv.idx", # Kallisto index file
        r1="../TESTS/{sample}_1.fastq", # Paired-end FASTQ files for each sample
        r2="../TESTS/{sample}_2.fastq" # Paired-end FASTQ files for each sample
    output:
        directory("../OUTPUTS/QUANT/{sample}")
    shell:
        """
        kallisto quant -i {input.index} -o {output} -b 30 {input.r1} {input.r2}
        """



# 4. Sleuth Differential Expression Analysis

rule sleuth_analysis:
    input:
        quant=expand("../OUTPUTS/QUANT/{sample}", sample=SAMPLES), # Kallisto quantification outputs for all samples
        script="../SCRIPTS/sleuth_analysis.R" # R script for performing Sleuth analysis
    output:
        "../OUTPUTS/TEMP/sleuth_report.txt" # Output report file for Sleuth analysis results
    shell:
        """
        mkdir -p ../OUTPUTS/TEMP
        Rscript {input.script} 
        """

# 5. Build Bowtie2 genome index

rule bowtie2_index:
    input:
        "../GCF_000845245.1/genomic.fna" # Genome FASTA file to be indexed
    output:
        expand("../OUTPUTS/BOWTIE2_INDEX/hcmv.{suffix}.bt2",
               suffix=["1", "2", "3", "4", "rev.1", "rev.2"]) # Bowtie2 index files (6 total)
    shell:
        """
        mkdir -p ../OUTPUTS/BOWTIE2_INDEX
        bowtie2-build {input} ../OUTPUTS/BOWTIE2_INDEX/hcmv
        """


# 6. Map reads using Bowtie2 (per sample)

rule mapping:
    input:
        index=expand("../OUTPUTS/BOWTIE2_INDEX/hcmv.{suffix}.bt2",
                     suffix=["1", "2", "3", "4", "rev.1", "rev.2"]),
        r1="../TESTS/{sample}_1.fastq",
        r2="../TESTS/{sample}_2.fastq"
    output:
        "../OUTPUTS/MAPPING/{sample}.bam"
    shell:
        """
        mkdir -p ../OUTPUTS/MAPPING

        bowtie2 -x ../OUTPUTS/BOWTIE2_INDEX/hcmv \
            -1 {input.r1} -2 {input.r2} \
        | samtools view -bS - \
        > {output}
        """



# 7. Count mapped reads
# (Output file is created inside the shell script)

rule count_reads:
    input:
        expand("../OUTPUTS/MAPPING/{sample}.bam", sample=SAMPLES)
    output:
        "../OUTPUTS/TEMP/count.txt"
    shell:
        """
        bash ../SCRIPTS/count_reads.sh
        """



# 8. SPAdes Assembly (per sample)

rule spades_assembly:
    input:
        r1="../TESTS/{sample}_1.fastq",
        r2="../TESTS/{sample}_2.fastq"
    output:
        "../OUTPUTS/SPADES/{sample}/transcripts.fasta"
    shell:
        """
        spades.py \
            --only-assembler \
            --rna \
            -k 127 \
            -1 {input.r1} \
            -2 {input.r2} \
            -o ../OUTPUTS/SPADES/{wildcards.sample}
        """



# 9. Extract Longest Contig (per sample)

rule longest_contig:
    input:
        "../OUTPUTS/SPADES/{sample}/transcripts.fasta"
    output:
        "../OUTPUTS/LONGEST_CONTIG/{sample}.fasta"
    shell:
        """
        mkdir -p ../OUTPUTS/LONGEST_CONTIG

        python ../SCRIPTS/longest_contig.py \
            -i {input} \
            -o {output}
        """

# 10. BLAST Longest Contig (per sample)

rule blast_longest_contig:
    input:
        query="../OUTPUTS/LONGEST_CONTIG/{sample}.fasta",
        db_files=expand("../BLAST/genome.{suffix}",
                        suffix=["nhr", "nin", "nsq"]) # BLAST database files
    output:
        report="../OUTPUTS/TEMP/{sample}.blast.txt", # BLAST output report for each sample
        done="../OUTPUTS/TEMP/blast_{sample}.done" # Flag file to indicate BLAST completion
    shell:
        """
        mkdir -p ../OUTPUTS/TEMP

        echo "{wildcards.sample}:" > {output.report}
        echo -e "sacc\tpident\tlength\tqstart\tqend\tsstart\tsend\tbitscore\tevalue\tstitle" >> {output.report}

        blastn \
            -query {input.query} \
            -db ../BLAST/genome \
            -max_hsps 1 \
            -max_target_seqs 5 \
            -outfmt "6 sacc pident length qstart qend sstart send bitscore evalue stitle" \
            >> {output.report}

        echo "" >> {output.report}
        touch {output.done}
        """



# 11. Merge Final Pipeline Report

rule merge_reports:
    input:
        MERGE_INPUTS # List of all intermediate report files to be merged
    output:
        "../PipelineReport.txt" # Final merged report containing all results
    shell:
        """
        cat {input} > {output}
        """